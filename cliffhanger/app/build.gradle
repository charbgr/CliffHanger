apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion rootProject.ext.compile_sdk_version
  buildToolsVersion rootProject.ext.build_tools_version

  defaultConfig {
    applicationId "com.github.charbgr.cliffhanger"
    minSdkVersion rootProject.ext.min_sdk_version
    targetSdkVersion rootProject.ext.target_sdk_version
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/rxjava.properties'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  buildTypes {
    applicationVariants.all { variant ->
      def secrets = rootProject.getSecrets()
      variant.buildConfigField "String", "TMDB_API_KEY", secrets['TMDB_API_KEY']
    }
  }

}


dependencies {
  def deps = rootProject.ext.mainApplication
  def unitTestDependencies = rootProject.ext.unitTesting

  // Android Stuff
  implementation deps.appCompat
  implementation deps.design
  implementation deps.constraintLayout

  // Reactive
  implementation deps.rxJava
  implementation deps.rxKotlin
  implementation deps.rxAndroid
  implementation deps.rxBinding
  implementation deps.rxBindingDesign
  implementation deps.rxBindingRecycler

  // Misc
  implementation deps.kotlin
  implementation deps.retrofit
  implementation deps.retrofitAdapter
  implementation deps.retrofitConverter
  implementation deps.okhttpInterceptor
  implementation deps.timber
  implementation deps.glide

  // Dagger
  implementation deps.dagger.runtime
  implementation deps.dagger.android.runtime
  implementation deps.dagger.android.supportRuntime
  kapt deps.dagger.compiler
  kapt deps.dagger.android.compiler

  // Modules
  implementation project(':api-tmdb')
  implementation project(':arch')
  implementation project(':baseadapter')
  implementation project(':domain')
  implementation project(':feature-browser')
  implementation project(':feature-error')
  implementation project(':feature-movie-detail')
  implementation project(':feature-search')
  implementation project(':shared-commands')
  implementation project(':shared-extensions')
  implementation project(':shared-movie-adapter')
  implementation project(':shared-views')


  // Unit/Integration tests dependencies
  testImplementation unitTestDependencies.kotlinTest
  testImplementation unitTestDependencies.mockitoKotlin
  testImplementation unitTestDependencies.kluent
  testImplementation unitTestDependencies.spek
//  testCompile unitTestDependencies.junit
  testImplementation unitTestDependencies.robolectric
}

repositories {
  mavenCentral()
  maven { url "http://dl.bintray.com/jetbrains/spek" }
}
